
@code{
  def genFutureMethodName(method) {
    return 'rx' + java.lang.Character.toUpperCase(method.name.charAt(0)) + method.name.substring(1);
  }

  def genFutureMethod(method) {
    var futMethodName = genFutureMethodName(method);
    var futParams = new java.util.ArrayList();
    var count = 0;
    var size = method.params.size() - 1;
    while (count < size) {
      var param = method.params[count];
      /* Transform ReadStream -> Flowable */
      futParams.add(param);
      count = count + 1;
    }
    var futParam = method.params[size];
    var futType = futParam.type.args[0].args[0];
    var futReturnType;
    if (futType.kind == CLASS_VOID) {
      futReturnType = io.vertx.codegen.type.TypeReflectionFactory.create(io.reactivex.Completable);
    } else {
      futReturnType = new io.vertx.codegen.type.ParameterizedTypeInfo(io.vertx.codegen.type.TypeReflectionFactory.create(io.reactivex.Single), false, java.util.Collections.singletonList(futType));
    }
    var futMethod = new io.vertx.codegen.MethodInfo(
      method.ownerTypes, futMethodName,
      method.kind,
      futReturnType,
      null,
      method.fluent,
      method.cacheReturn,
      futParams,
      method.comment,
      method.doc,
      method.staticMethod,
      method.defaultMethod,
      method.typeParams);
    return futMethod;
  }

  def genOverloadedMethod(method) {
    var params = null;
    var count = 0;
    for (param : method.params) {
      if (param.type.parameterized && param.type.raw.name == 'io.vertx.core.streams.ReadStream') {
        if (params == null) {
          params = new java.util.ArrayList(method.params);
        }
        var paramType = new io.vertx.codegen.type.ParameterizedTypeInfo(
          io.vertx.codegen.type.TypeReflectionFactory.create(io.reactivex.Flowable),
          false,
          java.util.Collections.singletonList(param.type.args[0])
        );
        params[count] = new io.vertx.codegen.ParamInfo(
          param.index,
          param.name,
          param.description,
          paramType
        );
      }
      count = count + 1;
    }
    if (params != null) {
      return new io.vertx.codegen.MethodInfo(
        method.ownerTypes,
        method.name,
        method.kind,
        method.returnType,
        null,
        method.fluent,
        method.cacheReturn,
        params,
        method.comment,
        method.doc,
        method.staticMethod,
        method.defaultMethod,
        method.typeParams);
    }
    return null;
  }
}

@declare{'genReadStream'}
  io.reactivex.Flowable<@{typeParams[0].name}> toFlowable();\n\n
@end{}

@declare{'genToObservable'}
	@code{streamType=type.raw.readStreamArg}
  private io.reactivex.Flowable<@{streamType.simpleName}> flowable;\n\n
  public synchronized io.reactivex.Flowable<@{streamType.simpleName}> toFlowable() {\n
    if (flowable == null) {\n
	@if{streamType.kind == CLASS_API}
      java.util.function.Function<@{streamType.raw.name}, @{streamType.simpleName}> conv = @{streamType.raw.simpleName}::newInstance;\n
      flowable = io.vertx.reactivex.RxHelper.toFlowable(delegate, conv);\n
	@else{streamType.variable}
      java.util.function.Function<Object, @{streamType.simpleName}> conv = (java.util.function.Function<Object, @{streamType.simpleName}>) __typeArg_0.wrap;\n
      flowable = io.vertx.reactivex.RxHelper.toFlowable(delegate, conv);\n
	@else{}
      flowable = io.vertx.reactivex.RxHelper.toFlowable(this.getDelegate());\n
	@end{}
    }\n
    return flowable;\n
  }\n\n
@end{}

@declare{'genRxMethod'}
	@code{futMeth = genFutureMethod(method)}
	@code{adapterType='io.vertx.reactivex.AsyncResult' + futMeth.returnType.simpleName}
	@code{rxType=futMeth.returnType.raw.name}
	@includeNamed{'startMethodTemplate';meth=futMeth;deprecated=''} { \n
    return @{rxType}.unsafeCreate(new @{adapterType}(handler -> {\n
      @{method.name}(@foreach{param:futMeth.params}@{param.name}@end{', '}@if{futMeth.params.size() > 0}, @end{}handler);\n
    }));\n
  }\n\n
@end{}

@declare{'genRxImports'}
import io.reactivex.Flowable;\n
import io.reactivex.Single;\n
import io.reactivex.Completable;\n
@end{}

@declare{'foobar'}
	@includeNamed{'genMethod'}
@end{}

@includeNamed{'class.templ';rxid='reactivex';rxpkg='rx'}
